{
package nqm_parser
}

Query = allParams:QueryParam* EOF {
	return toSetters(allParams), nil
}

QueryParam = _ param:(TimeFilter / NodeFilter) _ {
	return param, nil
} / paramName:ParamName ('=' ParamValue?)?  {
	return empty_param_setter, fmt.Errorf("Unknown parameter: %q", paramName)
}

NodeFilter = nodeProperty:NodeProperty '=' propertyValue:MultiLiteralString {
	return buildSetterFunc(nodeProperty, propertyValue)
} / paramName:NodeProperty assignedValue:('=' ParamValue)? END_WORD {
	return empty_param_setter, buildErrorForInvalidParam(paramName, assignedValue)
}

TimeFilter = timeParamName:TimeParamName '=' timeValue:(ISO_8601 / UNIX_TIME) {
	return buildSetterFunc(timeParamName, timeValue)
} / paramName:TimeParamName assignedValue:('=' ParamValue?)? END_WORD {
	return empty_param_setter, buildErrorForInvalidParam(paramName, assignedValue)
}

NodeProperty = ("agent" / "target") '.' ("isp" / "province" / "city") {
	return string(c.text), nil
}

TimeParamName = ("starttime" / "endtime") {
	return string(c.text), nil
}

ParamName = [^ =\t\n\r]+ {
	return string(c.text), nil
}
ParamValue = [^ \t\n\r]+ {
	return string(c.text), nil
}

MultiLiteralString = '%' autoCondition:LiteralStringForAutoCond '%' {
	return parseAutoCondition(autoCondition)
} / first:LiteralString rest:(RestLiteralString)* {
	return combineStringLiterals(first, rest), nil
}

LiteralStringForAutoCond = [^ \t\n\r,=%]+ {
	return string(c.text), nil
}
LiteralString = [^ \t\n\r,=]+ {
	return string(c.text), nil
}

RestLiteralString = ',' sValue:LiteralString {
	return sValue, nil
} / ',' errorLiteralValue:ParamValue {
	return "", fmt.Errorf("Illegal literal value: \"%v\"", errorLiteralValue)
}

ISO_8601 = [0-9]+ '-' [0-9]+ '-' [0-9]+ ('T' [0-9]+ (':' [0-9]+ ([Z+-]? [0-9]+ (':' [0-9]+)?)?)?)? {
	return parseIso8601(c)
}

UNIX_TIME = [0-9]+ {
	return parseUnixTime(c)
}

_ = EMPTY_CHAR*
END_WORD = EOF / EMPTY_CHAR+
EMPTY_CHAR = [ \t\n\r]
EOF = !.
